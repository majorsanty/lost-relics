plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'io.github.ladysnake.chenille' version '0.13.1'
    id 'org.jetbrains.kotlin.jvm'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

chenille {
    customLicense = rootProject.file("LICENSE")
    github = new URL("https://github.com/MoriyaShiine/lost-relics")
    javaVersion = 21
    configurePublishing {
        withLadysnakeMaven()
        withGithubRelease()
        withModrinthRelease()
        withCurseforgeRelease()
    }
}

modrinth {
    dependencies {
        required.project "fabric-api"
        required.project "trinkets"
        embedded.project "cardinal-components-api"
    }
    syncBodyFrom = rootProject.file("README.md").text
}

loom {
    accessWidenerPath = file("src/main/resources/lostrelics.accesswidener")
}

fabricApi {
    configureDataGeneration()
}

repositories {
    chenille.repositories.ladysnake()
    chenille.repositories.terraformers()
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"

    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.3.2")))

    modApi "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cardinal_components_version}"
    modApi "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cardinal_components_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    include modImplementation ("dev.emi:trinkets:${trinkets_version}")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()

}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}

loom {
    accessWidenerPath = file("src/main/resources/lostrelics.accesswidener")
    runs {
        datagenClient {
            inherit client
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=${archivesBaseName}"

            runDir "build/datagen"
        }
    }
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}
kotlin {
    jvmToolchain(17)
}